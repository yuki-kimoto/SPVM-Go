# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Go::Select {
  use Go::Select::Case;
  use Go::Select::Result;
  use Go::Channel;
  use List;
  use Fn;
  use Sys;
  
  # Fields
  has read_cases_h : Hash of Go::Channel;
  
  has write_cases_h : Hash of Go::Channel;
  
  has non_blocking : rw byte;
  
  has seed : int;
  
  # Class Methods
  static method new : Go::Select () {
    
    my $self = new Go::Select;
    
    $self->{read_cases_h} = Hash->new;
    
    $self->{write_cases_h} = Hash->new;
    
    $self->{seed} = Sys->process_id * (int)Sys->time;
    
    return $self;
  }
  
  # Instance Methods
  method add_read : void ($channel : Go::Channel) {
    
    unless ($channel) {
      die "\$channel must be defined";
    }
    
    my $read_cases_h = $self->{read_cases_h};
    
    my $address = Fn->to_address($channel);
    
    my $exists = $read_cases_h->exists($address);
    
    if ($exists) {
      die "\$channel is already exists";
    }
    
    my $case = Go::Select::Case->new;
    
    $case->{channel} = $channel;
    
    $read_cases_h->set($address, $case);
  }
  
  method add_write : void ($channel : Go::Channel, $value : object) {
    
    unless ($channel) {
      die "\$channel must be defined";
    }
    
    my $write_cases_h = $self->{write_cases_h};
    
    my $address = Fn->to_address($channel);
    
    my $exists = $write_cases_h->exists($address);
    
    if ($exists) {
      die "\$channel is already exists";
    }
    
    my $case = Go::Select::Case->new;
    
    $case->{channel} = $channel;
    
    $case->{value} = $value;
    
    $case->{is_write} = 1;
    
    $write_cases_h->set($address, $case);
  }
  
  method remove_read : void ($channel : Go::Channel) {
    
    unless ($channel) {
      die "\$channel must be defined";
    }
    
    my $read_cases_h = $self->{read_cases_h};
    
    my $address = Fn->to_address($channel);
    
    my $exists = $read_cases_h->exists($address);
    
    unless ($exists) {
      die "\$channel is not found";
    }
    
    $read_cases_h->delete($address);
  }
  
  method remove_write : void ($channel : Go::Channel) {
    
    unless ($channel) {
      die "\$channel must be defined";
    }
    
    my $write_cases_h = $self->{write_cases_h};
    
    my $address = Fn->to_address($channel);
    
    my $exists = $write_cases_h->exists($address);
    
    unless ($exists) {
      die "\$channel is not found";
    }
    
    $write_cases_h->delete($address);
  }
  
  method select : Go::Select::Result () {
    
    my $result = Go::Select::Result->new;
    
    my $read_cases_h = $self->{read_cases_h};
    
    my $write_cases_h = $self->{write_cases_h};
    
    my $read_cases_length = $read_cases_h->keys_length;
    
    my $write_cases_length = $write_cases_h->keys_length;
    
    my $cases_list = List->new(new Go::Select::Case[0]);
    
    for my $address (@{$read_cases_h->keys}) {
      my $read_case = $read_cases_h->get($address);
      
      $cases_list->push($read_case);
    }
    
    for my $address (@{$write_cases_h->keys}) {
      my $write_case = $write_cases_h->get($address);
      
      $cases_list->push($write_case);
    }
    
    my $cases_length = $cases_list->length;
    
    my $cases = $cases_list->to_array;
    
    my $seed = $self->{seed};
    
    &shuffle($cases, \$seed);
    
    $self->{seed} = $seed;
    
    
    return $result;
  }
  
  static method shuffle : void ($array : object[], $seed_ref : int*) {
    
    my $array_length = @$array;
    
    for (my $i = $array_length - 1; $i > 0; $i--) {
      
      my $j = Fn->crand($seed_ref) % ($i + 1);
      
      my $temp = $array->[$i];
      
      $array->[$i] = $array->[$j];
      
      $array->[$j] = $temp;
    }
  }
}
