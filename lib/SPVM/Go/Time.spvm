# Copyright (c) 2026 Yuki Kimoto
# MIT License

class Go::Time {
  version_from Go;
  
  use Sys::Time::Timespec;
  
  # Interfaces
  interface Cloneable;
  
  # Fields
  has real_timespec : Sys::Time::Timespec;
  
  has mono_timespec : Sys::Time::Timespec;
  
  # Class Methods
  static method NANOSECOND  : long () { return 1L; }
  
  static method MICROSECOND : long () { return 1_000L; }
  
  static method MILLISECOND : long () { return 1_000_000L; }
  
  static method SECOND      : long () { return 1_000_000_000L; }
  
  static method MINUTE      : long () { return 60_000_000_000L; }
  
  static method HOUR        : long () { return 3_600_000_000_000L; }
  
  static method unix : Go::Time ($sec : long, $nsec : long = 0) {
    
    my $self = new Go::Time;
    
    my $real_timespec = Sys::Time::Timespec->new;
    
    $real_timespec->set_tv_sec($sec);
    $real_timespec->set_tv_nsec($nsec);
    
    $self->{real_timespec} = $real_timespec;
    
    return $self;
  }
  
  # Instance Methods
  method add : Go::Time ($nanoseconds : long) {
    
    my $clone = $self->clone;
    
    my $nanoseconds_to_timespec = Sys::Time::Util->nanoseconds_to_timespec($nanoseconds);
    
    $clone->{real_timespec} = Sys::Time::Util->add_timespec($self->{real_timespec}, $nanoseconds_to_timespec);
    
    if ($self->{mono_timespec}) {
      $clone->{mono_timespec} = Sys::Time::Util->add_timespec($self->{mono_timespec}, $nanoseconds_to_timespec);
    }
    
    return $clone;
  }
  
  method clone : Go::Time () {
    
    my $clone = new Go::Time;
    
    $clone->{real_timespec} = &_timespec_clone($self->{real_timespec});
    
    if ($self->{mono_timespec}) {
      $clone->{mono_timespec} = &_timespec_clone($self->{mono_timespec});
    }
    
    return $clone;
  }
  
  private static method _timespec_clone : Sys::Time::Timespec ($timespec : Sys::Time::Timespec) {
    
    my $clone = Sys::Time::Timespec->new;
    
    $clone->set_tv_sec($timespec->tv_sec);
    
    $clone->set_tv_nsec($timespec->tv_nsec);
    
    return $clone;
  }
  
}
