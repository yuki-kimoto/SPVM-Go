# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Go::Schedule {
  allow Go::Schedule::Task;
  
  use Go::Coroutine;
  use Go::Queue;
  use Go::Schedule::Task;
  
  has coroutine_queue : Go::Queue of Go::Coroutine;
  
  has current_coroutine : Go::Coroutine;
  
  has task_coroutine : Go::Coroutine;
  
  has return_back : Go::Coroutine;
  
  static method new : Go::Schedule () {
    
    my $self = new Go::Schedule;
    
    my $coroutine_queue = Go::Queue->new;
    
    $self->{coroutine_queue} = $coroutine_queue;
    
    my $process_task = Go::Schedule::Task->new;
    
    $process_task->{schedule} = $self;
    
    my $process_task_coroutine = Go::Coroutine->new($process_task);
    
    $self->{task_coroutine} = $process_task_coroutine;
    
    return $self;
  }
  
  method schedule : void () {
    
    my $current_coroutine = $self->{current_coroutine};
    
    if ($current_coroutine) {
      $self->{coroutine_queue}->enqueue($current_coroutine);
    }
    else {
      $current_coroutine = Go::Coroutine->new;
      
      my $return_back = $self->{return_back};
      
      if ($return_back) {
        die "A return back already exists.";
      }
      
      $self->{return_back} = $current_coroutine;
    }
    
    my $process_task_coroutine = $self->{task_coroutine};
    
    Go::Coroutine->transfer($current_coroutine, $process_task_coroutine);
    
    $self->{current_coroutine} = undef;
    
  }
  
  method add_task : void ($task : Callback) {
    
    my $process_task_coroutine = $self->{task_coroutine};
    
    my $coroutine = Go::Coroutine->new($task, $process_task_coroutine);
    
    $self->{coroutine_queue}->enqueue($coroutine);
  }
  
  method DESTROY : void () {
    warn "DESTROY";
  }
}
