# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Go::Schedule {
  use Go::Coroutine;
  use Go::Process;
  use Go::Machine;
  use Go::Queue;
  use Go::Process::Task;
  use Go::Machine::ProcessTask;
  
  has current_machine : Go::Machine;
  
  static method new : Go::Schedule () {
    
    my $self = new Go::Schedule;
    
    my $process = Go::Process->new;
    
    $process->{schedule} = $self;
    
    my $process_task = Go::Process::Task->new;
    
    $process_task->{process} = $process;
    
    my $process_task_coroutine = Go::Coroutine->new($process_task);
    
    $process->{task_coroutine} = $process_task_coroutine;
    
    my $machine_process_task = Go::Machine::ProcessTask->new;
    
    $machine_process_task->{task} = $process_task;
    
    $machine_process_task->{schedule} = $process->{schedule};
    
    $machine_process_task->{process} = $process;
    
    $self->{current_machine} = $machine_process_task;
    
    return $self;
  }
  
  method schedule : void () {
    
    my $machine = $self->{current_machine};
    
    my $process = $machine->{process};
    
    my $current_coroutine = $process->{current_coroutine};
    
    if ($current_coroutine) {
      $process->{coroutine_queue}->enqueue($current_coroutine);
    }
    else {
      $current_coroutine = Go::Coroutine->new;
      
      my $return_back = $process->{return_back};
      
      if ($return_back) {
        die "A return back already exists.";
      }
      
      $process->{return_back} = $current_coroutine;
    }
    
    my $process_task_coroutine = $process->{task_coroutine};
    
    Go::Coroutine->transfer($current_coroutine, $process_task_coroutine);
  }
  
  method add_task : void ($task : Callback) {
    
    my $machine = $self->{current_machine};
    
    my $process = $machine->{process};
    
    my $process_task_coroutine = $process->{task_coroutine};
    
    my $coroutine = Go::Coroutine->new($task, $process_task_coroutine);
    
    $process->{coroutine_queue}->enqueue($coroutine);
  }
}
