# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Go::Schedule {
  allow Go;
  allow Go::Schedule::Task;
  
  use Go::Coroutine;
  use Go::Queue;
  use Go::Schedule::Task;
  use Sys::Time;
  use Sys::Time::Timeval;
  use Sys::Time::Timespec;
  use Go::Poll;
  
  has coroutine_queue : Go::Queue of Go::Coroutine;
  
  has current_coroutine : Go::Coroutine;
  
  has schedule_task_coroutine : Go::Coroutine;
  
  has poll : Go::Poll;
  
  private static method new : Go::Schedule () {
    
    my $self = new Go::Schedule;
    
    my $coroutine_queue = Go::Queue->new;
    
    $self->{coroutine_queue} = $coroutine_queue;
    
    $self->{poll} = Go::Poll->new;
    
    return $self;
  }
  
  private method schedule : void ($after : double = 0, $fd : int = -1, $is_write : int = 0) {
    
    my $current_coroutine = $self->{current_coroutine};
    
    if ($current_coroutine) {
      
      my $schedule_task_coroutine = $self->{schedule_task_coroutine};
      
      if ($after > 0) {
        
        my $now_tv = Sys::Time::Timeval->new;
        
        Sys::Time->gettimeofday($now_tv, undef);
        
        my $now_sec = $now_tv->tv_sec;
        
        my $now_nsec = 1000 * $now_tv->tv_sec;
        
        my $after_sec = (int)$after;
        
        my $after_nsec = (int)(($after - $after_sec) * 1_000_000_000);
        
        my $deadline_sec = $now_sec + $after_sec;
        
        my $deadline_nsec = $now_nsec + $after_nsec;
        
        if ($deadline_nsec >= 1_000_000_000) {
          $deadline_sec += 1;
          $deadline_nsec -= 1_000_000_000;
        }
        
        my $deadline_ts = Sys::Time::Timespec->new;
        
        $deadline_ts->set_tv_sec($deadline_sec);
        
        $deadline_ts->set_tv_nsec($deadline_nsec);
        
        $current_coroutine->{deadline} = $deadline_ts;
      }
      
      $current_coroutine->{fd} = $fd;
      
      $current_coroutine->{is_write} = (byte)$is_write;
      
      Go::Coroutine->transfer($current_coroutine, $schedule_task_coroutine);
    }
    else {
      if ($after > 0) {
        die "\$after(or \$timeout) must be given in a goroutine.";
      }
      
      my $return_back = Go::Coroutine->new;
      
      my $schedule_task = Go::Schedule::Task->new;
      
      $schedule_task->{schedule} = $self;
      
      my $schedule_task_coroutine = Go::Coroutine->new($schedule_task);
      
      $schedule_task_coroutine->{return_back} = $return_back;
      
      $self->{schedule_task_coroutine} = $schedule_task_coroutine;
      
      Go::Coroutine->transfer($return_back, $schedule_task_coroutine);
      
      $schedule_task->{schedule} = undef;
      
      $self->{schedule_task_coroutine} = undef;
      
      $self->{current_coroutine} = undef;
      
      $schedule_task = undef;
      
      $schedule_task_coroutine->{task} = undef;
    }
  }
  
  private method add_task : void ($task : Callback) {
    
    my $coroutine = Go::Coroutine->new($task);
    
    $self->{coroutine_queue}->enqueue($coroutine);
  }
  
}
