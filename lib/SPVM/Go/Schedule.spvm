# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Go::Schedule {
  allow Go;
  allow Go::Schedule::Task;
  allow Go::Channel;
  allow Go::Select;
  
  use Go::Coroutine;
  use List;
  use Go::Schedule::Task;
  use Sys::Time;
  use Sys::Time::Timeval;
  use Sys::Time::Timespec;
  use Go::Poll;
  
  has coroutines : List of Go::Coroutine;
  
  has current_coroutine : Go::Coroutine;
  
  has schedule_task_coroutine : Go::Coroutine;
  
  has poll : Go::Poll;
  
  # Class Methods
  private static method new : Go::Schedule () {
    
    my $self = new Go::Schedule;
    
    my $coroutines = List->new(new Go::Coroutine[0]);;
    
    $self->{coroutines} = $coroutines;
    
    $self->{poll} = Go::Poll->new;
    
    return $self;
  }
  
  private static method create_deadline : Sys::Time::Timespec ($after : double) {
    my $now_tv = Sys::Time::Timeval->new;
    
    Sys::Time->gettimeofday($now_tv, undef);
    
    my $now_sec = $now_tv->tv_sec;
    
    my $now_nsec = 1000 * $now_tv->tv_usec;
    
    my $after_sec = (int)$after;
    
    my $after_nsec = (int)(($after - $after_sec) * 1_000_000_000);
    
    my $deadline_sec = $now_sec + $after_sec;
    
    my $deadline_nsec = $now_nsec + $after_nsec;
    
    if ($deadline_nsec >= 1_000_000_000) {
      $deadline_sec += 1;
      $deadline_nsec -= 1_000_000_000;
    }
    
    my $deadline_ts = Sys::Time::Timespec->new;
    
    $deadline_ts->set_tv_sec($deadline_sec);
    
    $deadline_ts->set_tv_nsec($deadline_nsec);
    
    return $deadline_ts;
  }
  
  # Instance Methods
  private method schedule_io_read : int ($fd : int, $timeout : double = 0) {
    
    my $after = 0;
    
    my $is_write = 0;
    
    my $status = $self->schedule($after, $fd, $is_write, $timeout);
    
    return $status;
  }
  
  private method schedule_io_write : int ($fd : int, $timeout : double = 0) {
    
    my $after = 0;
    
    my $is_write = 1;
    
    my $status = $self->schedule($after, $fd, $is_write, $timeout);
    
    return $status;
  }
  
  private method schedule : int ($after : double = 0, $fd : int = -1, $is_write : int = 0, $timeout : double = 0) {
    
    my $status = 0;
    
    my $current_coroutine = $self->{current_coroutine};
    
    if ($current_coroutine) {
      
      my $schedule_task_coroutine = $self->{schedule_task_coroutine};
      
      unless ($after >= 0) {
        die "\$after must be greater than or equal to 0.";
      }
      
      unless ($after <= Fn->INT_MAX) {
        die "\$after must be less than or equal to Fn->INT_MAX.";
      }
      
      unless ($timeout >= 0) {
        die "\$timeout must be greater than or equal to 0.";
      }
      
      unless ($timeout <= Fn->INT_MAX) {
        die "\$timeout must be less than or equal to Fn->INT_MAX.";
      }
      
      if ($after > 0 && $timeout > 0) {
        die "Both \$after and $timeout cannot be greater than 0.";
      }
      
      if ($after > 0) {
        $current_coroutine->{deadline_timer} = &create_deadline($after);
      }
      
      $current_coroutine->{fd} = $fd;
      
      $current_coroutine->{is_write} = (byte)$is_write;
      
      Go::Coroutine->transfer($current_coroutine, $schedule_task_coroutine);
    }
    else {
      if ($after > 0) {
        die "\$after(or \$timeout) must be given in a goroutine.";
      }
      
      my $return_back = Go::Coroutine->new;
      
      my $schedule_task = Go::Schedule::Task->new;
      
      $schedule_task->{schedule} = $self;
      
      my $schedule_task_coroutine = Go::Coroutine->new($schedule_task);
      
      $schedule_task_coroutine->{return_back} = $return_back;
      
      $self->{schedule_task_coroutine} = $schedule_task_coroutine;
      
      Go::Coroutine->transfer($return_back, $schedule_task_coroutine);
      
      $schedule_task->{schedule} = undef;
      
      $self->{schedule_task_coroutine} = undef;
      
      $self->{current_coroutine} = undef;
      
      $schedule_task = undef;
      
      $schedule_task_coroutine->{task} = undef;
    }
    
    return $status;
  }
  
  private method add_task : void ($task : Callback) {
    
    my $coroutine = Go::Coroutine->new($task);
    
    $self->{coroutines}->push($coroutine);
  }
  
}
