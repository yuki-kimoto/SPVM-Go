# Copyright (c) 2026 Yuki Kimoto
# MIT License

class Go::Duration_1l : mulnum_t {
  version_from Go;
  
  use Sys::Time::Timespec;
  use Sys::Time::Util;
  use Fn;
  
  # Fields
  has nsec : long;
  
  # Class Method
  static method new : Go::Duration_1l ($nsec : long = 0) {
    
    my $duration : Go::Duration_1l;
    $duration->{nsec} = $nsec;
    
    return $duration;
  }
  
  static method new_from_timespec : Go::Duration_1l ($timespec : Sys::Time::Timespec) {
    
    my $duration_nsec = Sys::Time::Util->timespec_to_nanoseconds($timespec);
    
    &check_timespec_to_nanoseconds($timespec);
    
    my $duration = Go::Duration_1l->new($duration_nsec);
    
    return $duration;
  }
  
  private static method check_timespec_to_nanoseconds : void ($timespec : Sys::Time::Timespec) {
    
    my $max_duration_sec = (Fn->LONG_MAX / 1_000_000_000) - 1;
    
    unless ($timespec->tv_sec > -$max_duration_sec && $timespec->tv_sec < $max_duration_sec) {
      die "Sys::Time::Timespec \$timespec tv_sec must be greater than -$max_duration_sec and less than $max_duration_sec.";
    }
    
  }
  
  static method new_from_sec : Go::Duration_1l ($seconds : double) {
    
    my $timespec = Sys::Time::Util->float_seconds_to_timespec($seconds);
    
    my $duration_nsec = Sys::Time::Util->timespec_to_nanoseconds($timespec);
    
    my $duration = Go::Duration_1l->new($duration_nsec);
    
    return $duration;
  }
  
  static method to_timespec : Sys::Time::Timespec ($duration : Go::Duration_1l) {
    
    my $timespec = Sys::Time::Util->nanoseconds_to_timespec($duration->{nsec});
    
    return $timespec;
  }
  
  static method to_sec : double ($duration : Go::Duration_1l) {
    
    my $timespec = Sys::Time::Util->nanoseconds_to_timespec($duration->{nsec});
    
    my $seconds = Sys::Time::Util->timespec_to_float_seconds($timespec);
    
    return $seconds;
  }
  
}
