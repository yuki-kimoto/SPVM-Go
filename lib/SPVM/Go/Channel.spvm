# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Go::Channel {
  allow Go::Schedule;
  
  use Hash;
  use Go::Channel::Wait;
  
  # Fields
  has capacity : int;
  
  has length : int;
  
  has closed : int;
  
  has read_waiters : List of Go::Channel::Wait;
  
  has write_waiters : List of Go::Channel::Wait;
  
  has schedule : Go::Schedule;
  
  # Class Methods
  static method new : Go::Channel ($options : object[] = undef) {
    
    &check_option_names($options, ["capacity"]);
    
    my $options_h = Hash->new($options);
    
    my $self = new Go::Channel;
    
    my $capacity = $options_h->delete_or_default_int("capacity", 0);
    $self->{capacity} = $capacity;
    
    unless ($capacity >= 0) {
      die "The \"capacity\" option must be greater than or equal to 0.";
    }
    
    $self->{read_waiters} = List->new(new Go::Channel::Wait[0]);
    
    $self->{write_waiters} = List->new(new Go::Channel::Wait[0]);
    
    return $self;
  }
  
  private static method check_option_names : void ($options : object[], $available_option_names : string[]) {
    
    unless ($options) {
      return;
    }
    
    my $available_option_names_h = Hash->new;
    for my $available_option_name (@$available_option_names) {
     $available_option_names_h->set_int($available_option_name, 1);
    }
    
    for (my $i = 0; $i < @$options; $i += 2) {
      my $option_name = (string)$options->[$i];
      
      unless ($available_option_names_h->get($option_name)) {
        die "The \"$option_name\" option is not available.";
      }
    }
  }
  
  # Instance Methods
  method read : object ($error_ref : int*) {
    
    $$error_ref = 0;
    
    my $closed = $self->{closed};
    
    if ($closed) {
      $$error_ref = 1;
      return undef;
    }
    
    if ($self->{write_waiters}->length) {
      my $wait = (Go::Channel::Wait)$self->{write_waiters}->shift;
      
      my $coroutine = $wait->{coroutine};
      
      if ($coroutine) {
        $coroutine->{disable} = 0;
      }
      
      my $value = $wait->{value};
      
      return $value;
    }
    else {
      my $wait = Go::Channel::Wait->new;
      
      my $shcedule = $self->{schedule};
      
      my $current_coroutine = $shcedule->{current_coroutine};
      
      $current_coroutine->{disable} = 1;
      
      $wait->{coroutine} = $current_coroutine;
      
      $self->{read_waiters}->push($wait);
      
      $shcedule->schedule;
    }
    
    return undef;
  }
  
  method write : void ($value : object) {
    my $closed = $self->{closed};
    
    if ($closed) {
      die "This channel is closed.";
    }
    
    my $length = $self->{length};
    
    my $capacity = $self->{capacity};
    
    my $wait = Go::Channel::Wait->new;
    
    $wait->{value} = $value;
    
    $self->{write_waiters}->push($wait);
    
    if ($length < $capacity) {
      $self->{length}++;
    }
    else {
      my $shcedule = $self->{schedule};
      
      my $current_coroutine = $shcedule->{current_coroutine};
      
      $current_coroutine->{disable} = 1;
      
      $wait->{coroutine} = $current_coroutine;
      
      $shcedule->schedule;
    }
  }
  
  method close : void () {
    my $closed = $self->{closed};
    
    if ($closed) {
      die "This channel is already closed.";
    }
    
    $self->{closed} = 1;
  }
  
  method cap : int () {
    return $self->{capacity};
  }
  
  method len : int () {
    return $self->{length};
  }
  
}
