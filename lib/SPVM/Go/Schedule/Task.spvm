# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Go::Schedule::Task {
  version_from Go;
  allow Go::Schedule;
  
  use Sys::Time;
  use Sys::Time::Timespec;
  use Sys::Poll::PollfdArray;
  use Sys::Poll::Constant as POLL;
  use Sys::Poll;
  use Format;
  
  has schedule : Go::Schedule;
  
  private static method new : Go::Schedule::Task () {
    
    my $self = new Go::Schedule::Task;
    
    return $self;
  }
  
  private method : void () {
    
    my $schedule = $self->{schedule};
    
    my $poll = $schedule->{poll};
    
    my $loop_count = 0;
    while (1) {
      
      my $coroutines = $schedule->{coroutines};
      
      if ($coroutines->length == 0) {
        last;
      }
      
      my $check_io = $loop_count % 60 == 0;
      
      if ($check_io) {
        $poll->check;
      }
      else {
        my $coroutine = (Go::Coroutine)undef;
        
        if ($coroutines->length > 0) {
          $coroutine = (Go::Coroutine)$coroutines->get(0);
        }
        
        if ($coroutine->{finished}) {
          $coroutines->shift;
          
          $schedule->{current_coroutine} = undef;
          
          if (Go->ENV_DEBUG) {
            Fn->say_stderr(Format->sprintf("[Go Debug]End goroutine (Callback:%p, QueueNum:%d.)", [(object)$coroutine->{task}, $coroutines->length]));
          }
        }
        elsif ($coroutine->{disable}) {
          $coroutines->push($coroutines->shift);
          
          if (Go->ENV_DEBUG) {
            Fn->say_stderr(Format->sprintf("[Go Debug]Rotate IO goroutine (Callback:%p, QueueNum:%d.)", [(object)$coroutine->{task}, $coroutines->length]));
          }
        }
        else {
          my $is_over_deadline = 0;
          
          my $deadline_base_timer_go_time = $coroutine->{deadline_base_timer_go_time};
          
          if ($deadline_base_timer_go_time) {
            $is_over_deadline = &is_over_deadline($deadline_base_timer_go_time, $coroutine->{after_duration_nsec});
            
            if ($is_over_deadline) {
              $coroutine->{deadline_base_timer_go_time} = undef;
            }
            else {
              $coroutines->push($coroutines->shift);
              
              if (Go->ENV_DEBUG) {
                Fn->say_stderr(Format->sprintf("[Go Debug]Rotate timer goroutine (Callback:%p, QueueNum:%d)", [(object)$coroutine->{task}, $coroutines->length]));
              }
            }
          }
          else {
            $is_over_deadline = 1;
          }
          
          my $fd = $coroutine->{fd};
          if ($fd >= 0) {
            $coroutine->{disable} = 1;
            my $coroutine_address = Fn->to_address($coroutine);
            $schedule->{poll}->{coroutines_h}->set($coroutine_address, $coroutine);
            $coroutines->push($coroutines->shift);
            
            if (Go->ENV_DEBUG) {
              Fn->say_stderr(Format->sprintf("[Go Debug]Rotate IO goroutine first time (Callback:%p, QueueNum:%d.)", [(object)$coroutine->{task}, $coroutines->length]));
            }
          }
          else {
            if ($is_over_deadline) {
              
              my $schedule_task_coroutine = $schedule->{schedule_task_coroutine};
              
              $coroutines->push($coroutines->shift);
              
              $coroutine->{return_back} = $schedule_task_coroutine;
              
              $schedule->{current_coroutine} = $coroutine;
              
              Go::Coroutine->transfer($schedule_task_coroutine, $coroutine);
              
              if (Go->ENV_DEBUG) {
                Fn->say_stderr(Format->sprintf("[Go Debug]Start or resume goroutine (Callback:%p, QueueNum:%d.)", [(object)$coroutine->{task}, $coroutines->length]));
              }
            }
          }
        }
      }
      
      $loop_count++;
    }
  }
  
  private static method is_over_deadline : int ($deadline_base_go_time : Go::Time, $after_duration_nsec : long) {
    
    unless ($deadline_base_go_time) {
      die "\$deadline_base_go_time must be defined.";
    }
    
    my $deadline_go_time = $deadline_base_go_time->add(Go::Duration_1l->new($after_duration_nsec));
    
    my $now_go_time = Go::Time->now;
    
    my $is_over_deadline = $now_go_time->after($deadline_go_time);
    
    return $is_over_deadline;
  }
  
}
