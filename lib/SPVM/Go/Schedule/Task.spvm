# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Go::Schedule::Task {
  allow Go::Schedule;
  
  use Sys::Time;
  use Sys::Time::Timeval;
  use Sys::Time::Timespec;
  use Sys::Poll::PollfdArray;
  use Sys::Poll::Constant as POLL;
  use Sys::Poll;
  
  has schedule : Go::Schedule;
  
  private static method new : Go::Schedule::Task () {
    
    my $self = new Go::Schedule::Task;
    
    return $self;
  }
  
  private method : void () {
    
    my $now_tv = Sys::Time::Timeval->new;
    
    my $schedule = $self->{schedule};
    
    my $poll = $schedule->{poll};
    
    my $loop_count = 0;
    while (1) {
      
      my $coroutines = $schedule->{coroutines};
      
      if ($coroutines->length == 0) {
        last;
      }
      
      my $check_io = $loop_count % 60 == 0;
      
      if ($check_io) {
        
        my $ready_coroutines = $poll->poll;
        
        for my $ready_coroutine (@$ready_coroutines) {
          $ready_coroutine->{disable} = 0;
          $ready_coroutine->{fd} = -1;
        }
      }
      else {
        my $coroutine = (Go::Coroutine)undef;
        
        if ($coroutines->length > 0) {
          $coroutine = (Go::Coroutine)$coroutines->shift;
        }
        
        if ($coroutine->{finished}) {
          $schedule->{current_coroutine} = undef;
        }
        elsif ($coroutine->{disable}) {
          $coroutines->push($coroutine);
        }
        else {
          my $over_deadline = 0;
          
          my $deadline_ts = $coroutine->{deadline};
          if ($deadline_ts) {
            
            Sys::Time->gettimeofday($now_tv, undef);
            
            my $now_sec = $now_tv->tv_sec;
            my $now_nsec = 1000 * $now_tv->tv_usec;
            
            my $deadline_sec = $deadline_ts->tv_sec;
            my $deadline_nsec = $deadline_ts->tv_nsec;
            
            if ($now_sec > $deadline_sec || ($now_sec == $deadline_sec && $now_nsec > $deadline_nsec)) {
              $coroutine->{deadline} = undef;
              $over_deadline = 1;
            }
            else {
              $coroutines->push($coroutine);
            }
          }
          else {
            $over_deadline = 1;
          }
          
          my $fd = $coroutine->{fd};
          if ($fd >= 0) {
            $coroutine->{disable} = 1;
            my $coroutine_address = Fn->to_address($coroutine);
            $schedule->{poll}->{coroutines_h}->set($coroutine_address, $coroutine);
            $coroutines->push($coroutine);
          }
          else {
            if ($over_deadline) {
              
              my $schedule_task_coroutine = $schedule->{schedule_task_coroutine};
              
              $coroutines->push($coroutine);
              
              $coroutine->{return_back} = $schedule_task_coroutine;
              
              $schedule->{current_coroutine} = $coroutine;
              
              Go::Coroutine->transfer($schedule_task_coroutine, $coroutine);
            }
          }
        }
      }
      
      $loop_count++;
    }
  }
  
}
