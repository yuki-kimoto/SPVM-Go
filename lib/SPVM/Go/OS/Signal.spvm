# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Go::OS::Signal {
  version_from Go;
  use Go;
  use Go::Channel;
  use Sys::Signal;
  use Sys::IO::Constant as IO;
  use Sys;
  use Errno;
  use Thread;
  use Go::Sync::WaitGroup;
  use Hash;
  
  # Class Variables
  our $SIGNAL_READ_FD : int;
  
  our $SIGNAL_WRITE_FD : int;
  
  our $NOTIFIED_SIGNALS_H : cache Hash of Go::Channel[];
  
  our $SIGNAL_HANDLER_RUNNING : byte;
  
  our $ORIGINAL_SIGNAL_HANDLERS_H : cache Hash of Int;
  
  static method ignore : void ($signal : int) {
    
    $NOTIFIED_SIGNALS_H->delete("$signal");
  }
  
  static method notify : void ($channel : Go::Channel, $signal : int) {
    
    unless ($channel) {
      die "\$channel must be defined.";
    }
    
    my $original_signal_hander = Sys::Signal->signal($signal, Sys::Signal->SIG_GO);
    
    $NOTIFIED_SIGNALS_H //= Hash->new;
    
    unless ($NOTIFIED_SIGNALS_H->{"$signal"}) {
      $NOTIFIED_SIGNALS_H->{"$signal"} = new Go::Channel[0];
      $ORIGINAL_SIGNAL_HANDLERS_H //= Hash->new;
      $ORIGINAL_SIGNAL_HANDLERS_H->{"$signal"} = $original_signal_hander;
    }
    
    List->new_ref($NOTIFIED_SIGNALS_H->{"$signal"})->push($channel);
    
    unless (&is_handler_running) {
      &start_handler;
    }
    
    Go->go(method : void () {
      
      my $buffer = (mutable string)new_string_len 4;
      
      my $thread = Thread->new([$buffer : mutable string] method : void () {
        Sys::IO->read($SIGNAL_READ_FD, $buffer, 4);
      });
      
      $thread->join;
      
      my $numbers = new int [1];
      
      Fn->memcpy($numbers, 0, $buffer, 0, 4);
      
      my $got_signal = $numbers->[0];
      
      if (my $channels = $NOTIFIED_SIGNALS_H->{"$got_signal"}) {
        for my $channel (@$channels) {
          my $select = Go->new_select;
          $select->set_non_blocking(1);
          $select->add_write($channel, $got_signal);
          $select->select;
        }
      }
    });
  }
  
  static method stop : void ($channel : Go::Channel, $signal : int) {
    
    my $channels = $NOTIFIED_SIGNALS_H->{"$signal"} // new Go::Channel[0];
    my $new_channels = (List of Go::Channel)List->new(new Go::Channel[0]);
    for my $_ (@$channels) {
      unless ($_ == $channel) {
        $new_channels->push($_);
      }
    }
    
    $NOTIFIED_SIGNALS_H->{"$signal"} = $new_channels->to_array;
  }
  
  static method start_handler : void () {
    
    if ($SIGNAL_HANDLER_RUNNING) {
      die "Go::OS::Signal handler is already running.";
    }
    
    $SIGNAL_HANDLER_RUNNING = 1;
    
    my $signal_read_fd = -1;
    my $signal_write_fd = -1;
    
    Sys->pipe(\$signal_read_fd, \$signal_write_fd);
    
    $SIGNAL_READ_FD = $signal_read_fd;
    
    $SIGNAL_WRITE_FD = $signal_write_fd;
    
    Sys::Signal->SET_SIG_GO_WRITE_FD($signal_write_fd);
  }
  
  static method stop_handler : void () {
    
    unless ($SIGNAL_HANDLER_RUNNING) {
      die "Go::OS::Signal handler is already stopped.";
    }
    
    $SIGNAL_HANDLER_RUNNING = 0;
  }
  
  static method is_handler_running : int () {
    
    return $SIGNAL_HANDLER_RUNNING;
  }
  
}
