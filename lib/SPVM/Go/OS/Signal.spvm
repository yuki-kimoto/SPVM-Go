# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Go::OS::Signal {
  use Go;
  use Go::Channel;
  use Sys::Signal;
  use Sys::IO::Constant as IO;
  use Sys;
  use Errno;
  
  INIT {
    my $fds = [-1, -1];
    
    Sys->pipe($fds);
    
    $READ_FD = $fds->[0];
    $WRITE_FD = $fds->[1];
    
    Sys->fcntl($READ_FD, IO->F_SETFL, IO->O_NONBLOCK);
  }
  
  our $READ_FD : int;
  
  our $WRITE_FD : int;
  
  static method ignore : void ($signal : int) {
    Sys::Signal->signal($signal, Sys::Signal->SIG_IGN);
  }
  
  static method notify : void ($channel : Go::Channel, $signal : int) {
    
    unless ($channel) {
      die "\$channel must be defined.";
    }
    
    Sys::Signal->signal_io($signal, $WRITE_FD);
    
    Go->go([$channel : Go::Channel] method : void () {
      my $buffer = (mutable string)new_string_len 4;
      while (1) {
        eval { Sys::IO->read($READ_FD, $buffer, 4); }
        
        if ($@) {
          if (Errno->errno == Errno->EWOULDBLOCK || Errno->errno == Errno->EINTR) {
            Go->gosched_io_read($READ_FD);
          }
          else {
            die $@;
          }
        }
        else {
          last;
        }
      }
      
      my $numbers = new int [1];
      
      Fn->memcpy($numbers, 0, $buffer, 0, 4);
      
      my $got_signal = $numbers->[0];
      
      $channel->write($got_signal);
    });
  }
}
