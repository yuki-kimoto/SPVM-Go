# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Go::Sync::WaitGroup {
  use Go;
  use Sync::Mutex;
  
  has count : int;
  
  has mutex : Sync::Mutex;
  
  # Class Methods
  static method new : Go::Sync::WaitGroup () {
    
    my $self = new Go::Sync::WaitGroup;
    
    $self->{mutex} = Sync::Mutex->new;
    
    return $self;
  }
  
  # Instance Methods
  method add : void ($delta : int = 1) {
    
    my $mutex = $self->{mutex};
    
    {
      $mutex->lock;
      
      my $count = $self->{count};
      
      $count += $delta;
        
      my $count_is_negative = $count < 0;
      
      if ($count_is_negative) {
        die "The count field must be greater than or equal to 0.";
      }
      
      $self->{count} = $count;
      
      $mutex->unlock;
    }
  }
  
  method done : void () {
    $self->add(-1);
  }
  
  method wait : void () {
    
    while (1) {
      my $count = $self->{count};
      
      if ($count == 0) {
        return;
      }
      
      Go->gosched;
    }
  }
}
