class TestCase::Go {
  use Go;
  use Sys;
  use Point;
  
  our $RESULT : IntList;
  
  static method go_minimal : int () {
    
    $TestCase::Go::RESULT = IntList->new;
    
    Go->go(method : void () {
      $TestCase::Go::RESULT->push(2);
    });
    
    $TestCase::Go::RESULT->push(1);
    
    Go->gosched;
    
    $TestCase::Go::RESULT->push(3);
    
    unless (Array->equals_int($TestCase::Go::RESULT->to_array, [1, 2, 3])) {
      return 0;
    }
    
    $TestCase::Go::RESULT = undef;
    
    return 1;
  }
  
  static method go_basic : int () {
    
    $TestCase::Go::RESULT = IntList->new;
    
    Go->go(method : void () {
      
      $TestCase::Go::RESULT->push(2);
      
      Go->go(method : void () {
        $TestCase::Go::RESULT->push(4);
      });
      
      $TestCase::Go::RESULT->push(3);
    });
    
    $TestCase::Go::RESULT->push(1);
    
    Go->gosched;
    
    $TestCase::Go::RESULT->push(5);
    
    unless (Array->equals_int($TestCase::Go::RESULT->to_array, [1, 2, 3, 4, 5])) {
      return 0;
    }
    
    $TestCase::Go::RESULT = undef;
    
    return 1;
  }
  
  static method go_die : int () {
    
    $TestCase::Go::RESULT = IntList->new;
    
    Go->go(method : void () {
      die "Exception!.";
      
      $TestCase::Go::RESULT->push(1);
      
    });
    
    Go->go(method : void () {
      eval {
        die "Exception!.";
      }
      
      $TestCase::Go::RESULT->push(2);
      
    });
    
    Go->go(method : void () {
      TestCase::Go->error();
      
      $TestCase::Go::RESULT->push(3);
      
    });
    
    Go->gosched;
    
    unless (Array->equals_int($TestCase::Go::RESULT->to_array, [2])) {
      return 0;
    }
    
    $TestCase::Go::RESULT = undef;
    
    return 1;
  }
  
  static method error : void () {
    
    die "Error Method";
  }
  
  static method go_extra : int () {
    
    Go->go(method : void () {});
    
    Go->gosched;
    
    Go->gosched;
    
    return 1;
  }
  
  static method sleep : int () {
    
    Go->go(method : void () {
      
      my $seconds = 1.5;
      
      Go->sleep($seconds);
    });
    
    Go->gosched;
    
    return 1;
  }
}
